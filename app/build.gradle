buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        //noinspection GradleDynamicVersion
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'

static def execAndCaptureOutput(String cmd) {
    def output = ''
    try {
        def proc = cmd.execute()
        proc.in.eachLine { line -> output = line }
        proc.err.eachLine { line -> println line }
        proc.waitFor()
        if (proc.exitValue() != 0) {
          output = ''
        }
    } catch (Exception e) {
        // Do nothing
        println e
    }
    return output
}

static def gitBranch() {
    return execAndCaptureOutput('git rev-parse --abbrev-ref HEAD')
}

static def gitSha1() {
    return execAndCaptureOutput('git rev-parse --short HEAD')
}

static def buildTime() {
  return new Date().format('yyMMdd-HHmm')
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    dataBinding {
        enabled = true
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28

        renderscriptTargetApi 28
        vectorDrawables.useSupportLibrary = true

        applicationId 'com.poupa.vinylmusicplayer'
        versionCode 167
        versionName '0.22.1'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG '
            testCoverageEnabled true
        }
    }
    signingConfigs {
        circleci {
            storeFile file("../.circleci/circleci-unsecured.jks")
            keyAlias = "phono-unsecured"
            storePassword = "phono-unsecured"
            keyPassword = "phono-unsecured"
        }
    }
    flavorDimensions "default"
    productFlavors {
        playStore {
            dimension "default"
        }
        fdroid {
            dimension "default"
        }
        circleci {
            dimension "default"
            signingConfig signingConfigs.circleci
            def buildId = "_" + buildTime() + "_" + gitSha1()
            versionNameSuffix buildId
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "androidx.core:core:1.0.2"
    implementation "androidx.legacy:legacy-support-core-utils:1.0.0"
    implementation "androidx.legacy:legacy-support-core-ui:1.0.0"
    implementation "androidx.media:media:1.0.1"
    implementation "androidx.fragment:fragment:1.0.0"
    implementation "androidx.legacy:legacy-support-v13:1.0.0"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.palette:palette:1.0.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.annotation:annotation:1.0.2"
    implementation "androidx.percentlayout:percentlayout:1.0.0"
    implementation "androidx.preference:preference:1.0.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation 'com.github.kabouzeid:app-theme-helper:1.3.10'
    implementation 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    implementation 'com.github.kabouzeid:SeekArc:1.2-kmod'
    implementation 'com.github.kabouzeid:AndroidSlidingUpPanel:3.3.3-kmod'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.afollestad:material-cab:0.1.12'
    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.0.0'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0'
    implementation 'com.heinrichreimersoftware:material-intro:1.6'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
    implementation 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5'
    implementation 'com.jakewharton:butterknife:10.1.0'
    implementation 'com.github.AdrienPoupa:jaudiotagger:2.2.3'
    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0@aar') {
        transitive = true
    }
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
